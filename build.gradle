group 'j-dump2csv'
version '1.0.2'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8
mainClassName = 'org.dump2csv.Dumper'
applicationDefaultJvmArgs = ["-Dorg.dump2csv.Dumper.version=" + version]

repositories {
    mavenCentral()
}

dependencies {
    compile files('libs/ojdbc7.jar')
    compile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    compile group: 'com.h2database', name: 'h2', version: '1.4.197'
    compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.11'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.3'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.5'
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
//    compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    testCompile files('libs/ojdbc7.jar')
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// excluding Oracle drivers from distribution
distZip {
    exclude("ojdbc7.jar")
}
distTar {
    exclude("ojdbc7.jar")
}

applicationDistribution.from("src/support/install") {
    applicationDistribution.from("src/support/samples") {
        into "./samples"
    }
    applicationDistribution.from("src/support/vagrant_mysql") {
        into "./vagrant_mysql"
    }
    applicationDistribution.from("docs") {
        into "./docs"
    }
    filter { String line ->
        line.startsWith('version=') ? "version=\"${version}\"" : line
    }
}

// * This is a support task for testing contents of tar, run it from terminal
// *     $ sh ./gradlew checkTar
task checkTar(type: Exec){
    commandLine 'sh', 'src/support/checkinstall', projectDir, buildDir, version
}

test {
    exclude '**/integration/*'
}

task testMySQL (type: Test){
    outputs.upToDateWhen { false }
    systemProperty "test.myslq.user", "testuser"
    systemProperty "test.myslq.password", "test"
    systemProperty "test.myslq.uri","jdbc:mysql://localhost:13306/"
    systemProperty "test.myslq.database", "testdb"
}

task testOracle (type: Test){
    outputs.upToDateWhen { false }
    systemProperty "test.oracle.user", "testuser"
    systemProperty "test.oracle.password", "test"
    systemProperty "test.oracle.uri","jdbc:mysql://localhost:13306/"
    systemProperty "test.oracle.database", "testdb"
}

